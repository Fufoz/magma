#version 450

#include "quaternion.h.glsl"
#include "debug.h.glsl"

struct BoidState
{
	vec4 orientation;
	vec4 position;
	vec4 direction;
	vec4 up;
};

struct Plane
{
	vec4 normal;
	vec4 point;
};

struct Ray
{
	vec3 origin;
	vec3 direction;
};

layout(push_constant) uniform BoidsCommon
{
	float minDistance;
	float flockRadius;
	float tankSize;
	float deltaTime;
	int   boidsCount;
	int   spherePointsCount;
}Common;

layout(std430, set = 0, binding = 0) buffer BoidsState {
	BoidState states[];
};

//later gets passed to the vertex shader
layout(std430, set = 0, binding = 1) writeonly buffer InstanceInfos {
	mat4 instanceTransforms[];
};

layout(std430, set = 0, binding = 2) buffer SpherePoints {
	vec4 spherePoints[];
};

layout(set = 0, binding = 3) uniform TankPlanes {
	Plane planes[6];
}tankPlanes;

//later gets passed as input to debug vertex shader
layout(std430, set = 0, binding = 4) writeonly buffer DebugVectors {
	DebugInfo debugVectors[];
};

layout(local_size_x_id = 100) in;


float distanceBetween(vec3 first, vec3 second)
{
	float xdelta = second.x - first.x;
	float ydelta = second.y - first.y;
	float zdelta = second.z - first.z;

	return sqrt(xdelta * xdelta + ydelta * ydelta + zdelta * zdelta);
}

vec3 separation(BoidState currentBoid, uint currentBoidId)
{
	vec3 shiftDir = vec3(0.0, 0.0, 0.0);
	int count = 0;
	for(int i = 0; i < Common.boidsCount; i++)
	{
		if(i != currentBoidId)
		{
			float d = distanceBetween(currentBoid.position.xyz, states[i].position.xyz);
			if(d < Common.minDistance)
			{
				shiftDir += (currentBoid.position - states[i].position).xyz;
				count++;
			}
		}
	}

	return shiftDir;
}

//move to the average center mass of nearby boids
vec3 cohesion(BoidState currentBoid, uint currentBoidId)
{
	vec3 cohesionDir = vec3(0.0, 0.0, 0.0);
	float viewRadius = 10.f;
	int boidsCount = 0;
	vec3 positions = vec3(0.0, 0.0, 0.0);
	for(int i = 0; i < Common.boidsCount; i++)
	{
		if(i != currentBoidId)
		{
			if(distanceBetween(currentBoid.position.xyz, states[i].position.xyz) < viewRadius)
			{
				positions += states[i].position.xyz;
				boidsCount++;
			}
		}
	}

	vec3 avgPos = positions / boidsCount;

	cohesionDir = avgPos - currentBoid.position.xyz;
	return cohesionDir;
}

//align velocity
vec3 alignment(BoidState currentBoid, uint currentBoidId)
{
	vec3 alignmentDir = vec3(0.0, 0.0, 0.0);
	vec3 directions   = vec3(0.0, 0.0, 0.0);
	float viewRadius  = 10.f;
	int boidsCount    = 0;

	for(int i = 0; i < Common.boidsCount; i++)
	{
		if(i != currentBoidId)
		{
			if(distanceBetween(currentBoid.position.xyz, states[i].position.xyz) < viewRadius)
			{
				directions += states[i].direction.xyz;
				boidsCount++;
			}
		}
	}

	vec3 avgVelocityDir = directions / boidsCount;

	alignmentDir = avgVelocityDir - currentBoid.direction.xyz;
	return alignmentDir;
}

float planeSDF(Plane plane, vec3 point)
{
	return dot(point - plane.point.xyz, plane.normal.xyz);
}

bool sphereCast(Ray ray, float sphereRadius, float maxDistance, out float closestDistance)
{
	float t = 1.f;
	float delta = 0.1f;

	int planeCount = 6;
	float minDistance = maxDistance;//max float
	closestDistance = maxDistance;
	while(t < maxDistance)
	{
		float sphereDelta = 0.f;
		for(int i = 0; i < planeCount; i++)
		{
			float distance = planeSDF(tankPlanes.planes[i], ray.origin + ray.direction * (t));

			if(distance > 0 && distance < minDistance)
			{
				minDistance = distance;
				closestDistance = distance;
				sphereDelta = distance;
			}
		}

		t += max(delta, sphereDelta);
	}

	if(minDistance < sphereRadius)
	{
		return true;
	}

	return false;
}


bool isAboutToCollide(BoidState boid)
{
	Ray forwardRay;
	forwardRay.origin = boid.position.xyz;
	forwardRay.direction = boid.direction.xyz;
	float minDistance = 0.f;
	return sphereCast(forwardRay, 3.f, Common.minDistance, minDistance);
}

vec3 findNonBlockingDirection(BoidState boid)
{
	Ray forwardRay;
	forwardRay.origin = boid.position.xyz;
	forwardRay.direction = boid.direction.xyz;
	
	Ray bestRay = forwardRay;
	
	mat4 rotMat = quatToRotationMat(boid.orientation);
	float maxDistance = 0;
	// int discardCount = 0;

	for(int i = 0; i < Common.spherePointsCount; i++)
	{
		Ray currentRay;
		currentRay.origin = boid.position.xyz;
		currentRay.direction = (rotMat * spherePoints[i]).xyz;

		//discard rays that are behind fish forward direction
		if(dot(forwardRay.direction, currentRay.direction) < -0.5)
		{
			continue;
		}
	
		float hitDistance = 0.f;
		if(sphereCast(currentRay, 3.f, Common.minDistance, hitDistance))
		{
			if(hitDistance > maxDistance)
			{
				maxDistance = hitDistance;
				bestRay = currentRay;
			}
		}
		else
		{
			return currentRay.direction;
		}
	}

	return bestRay.direction;
}

void fillDebugData(uint gx, vec3 nonBlockDir)
{
	vec4 upColor = vec4(1.0, 0.0, 0.0, 0.0);
	vec4 forwardColor = vec4(0.0, 1.0, 0.0, 0.0);
	vec4 nonBlockColor = vec4(0.494, 0.509, 0.925, 0.0);

	float scaleCoeff = 3.f;

	debugVectors[gx*6].linePoint = states[gx].position;
	debugVectors[gx*6].color = upColor;
	debugVectors[gx*6 + 1].linePoint = states[gx].position + scaleCoeff*states[gx].up;
	debugVectors[gx*6 + 1].color = upColor;

	debugVectors[gx*6 + 2].linePoint = states[gx].position;
	debugVectors[gx*6 + 2].color = forwardColor;
	debugVectors[gx*6 + 3].linePoint = states[gx].position + scaleCoeff*states[gx].direction;
	debugVectors[gx*6 + 3].color = forwardColor;


	debugVectors[gx*6 + 4].linePoint = states[gx].position;
	debugVectors[gx*6 + 4].color = nonBlockColor;
	debugVectors[gx*6 + 5].linePoint = states[gx].position + scaleCoeff*vec4(nonBlockDir, 1.0);
	debugVectors[gx*6 + 5].color = nonBlockColor;
}

vec3 steer(vec3 currentDirection, vec3 desiredDirection, float weight)
{
	return weight * (desiredDirection - currentDirection);
}

void main()
{	
	uint gx = gl_GlobalInvocationID.x;
	if(gx > Common.boidsCount)
	{
		return;
	}
	vec3 previousDirection = states[gx].direction.xyz;
	vec3 newDirection = previousDirection;
	float step = 0.1f;
	
	
	vec3 forwardOld = states[gx].direction.xyz;
	
	vec3 separationDir = vec3(0.0, 0.0, 0.0);
	vec3 cohesionDir = vec3(0.0, 0.0, 0.0);
	vec3 alignmentDir = vec3(0.0, 0.0, 0.0);
	vec3 nonBlockingDir = vec3(0.0, 0.0, 0.0);

	separationDir = steer(previousDirection, separation(states[gx], gx), 0.01f);
	cohesionDir = steer(previousDirection, cohesion(states[gx], gx), 0.001f);//0.005
	alignmentDir = steer(previousDirection, alignment(states[gx], gx), 0.07f);
	
	if(isAboutToCollide(states[gx]))
	{
		nonBlockingDir = steer(previousDirection, findNonBlockingDirection(states[gx]), 0.15f);
	}

	newDirection += (separationDir + alignmentDir + cohesionDir + nonBlockingDir);
	vec4 test = rotateFromTo(previousDirection, newDirection);
	
	states[gx].orientation = quatMultiply(rotateFromTo(previousDirection, newDirection), states[gx].orientation);
	
	mat4 rotMat = quatToRotationMat(rotateFromTo(previousDirection, newDirection));


	states[gx].direction = rotMat * vec4(forwardOld, 1.0);
	states[gx].up = rotMat * states[gx].up;

	states[gx].position += step * (states[gx].direction);
	
	fillDebugData(gx, newDirection);

	
	instanceTransforms[gx] = 
		loadTranslation(states[gx].position.xyz) *
		quatToRotationMat(states[gx].orientation);

}